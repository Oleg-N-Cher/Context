// TinyContext language by Andrei Khokhlov - avhohlov.narod.ru
// Adapted for AgonLight (eZ80) by Oleg N. Cher - zx.oberon.org

  char  Text  [ 4096];
  word pText;
  word nText;
  word nLine;

  word aCode;         // Code start address
  word aData;         // Data start address
  byte  Code  [16384];
  word nCode;

  byte hFile;
  word  SP;

  char  Heap  [ 2048];
  word nHeap;

  word  Name  [  144];
  word  Cls   [  144];
  word  Sub   [  144];
  word  Type  [  144];
  word  Size  [  144];
  word  Ofs   [  144];
  word nName;

  word nData;

  word  Stk   [  128];
  word pStk;

  char  Buff  [  128];

  char  Src   [   64];
  char  Dest  [   64];
  char Command [  64];


  byte open () is
    inline 0x21, Src;        // address of filename (0-terminated)
    inline 0x0E, 0x01;       // LD   C, fa_read           
    inline 0x3E, 0x0A;       // MOSCALL mos_fopen
    inline 0x49, 0xCF;       // RST.LIS 08H
    inline 0x5F;             // LD   E, A  ; returns filehandle
  end


  word read () is
                             // read (hFile, @Text[pText], nTEXT)
    inline 0x3A, hFile;      // LD   A, (hFile)
    inline 0x4F;             // LD   C, A
    inline 0x5B, 0x11,Text,4;// LD.LIL DE, Text
    inline 0x2A, pText;      // LD   HL, (pText)
    inline 0x5B, 0x19;       // ADD.LIL HL, DE
    inline 0x11, 0x00, 0x10; // LD   DE, 4096 ; nTEXT
    inline 0x3E, 0x1A;       // MOSCALL mos_fread
    inline 0x49, 0xCF;       // RST.LIS 08H ; DEU: number of bytes read
  end


  byte save () is
    inline 0x21, Dest;       // address of filename (0-terminated)
    inline 0xE5;             // PUSH HL
    inline 0x3E, 0x05;       // MOSCALL mos_del
    inline 0x49, 0xCF;       // RST.LIS 08H
    inline 0xE1;             // POP HL
    inline 0x11, Code;       // address to save from
    inline 0xED, 0x4B, nCode;// number of bytes to save
    inline 0x3E, 0x02;       // MOSCALL mos_save
    inline 0x49, 0xCF;       // RST.LIS 08H
    inline 0x5F;             // LD   E, A  ; file error, or 0 if OK
    inline 0x16, 0;          // LD   D, 0
  end


  byte close () is
    inline 0x3A, hFile;      // LD   A, (hFile)
    inline 0x4F;             // LD   C, A
    inline 0x3E, 0x0B;       // MOSCALL mos_fclose
    inline 0x49, 0xCF;       // RST.LIS 08H ; A: Number of files still open
    inline 0x5F;             // LD   E, A
  end


  word putc (char C) is
    inline 0x7B;             // LD   A, E
    inline 0x49, 0xD7;       // RST.LIS 10H
  end


  word puts (word S) is
    inline 0xEB;             // EX   DE, HL ; Address of string
    inline 0xAF;             // XOR A ; Use char in A as delimiter
    inline 0x4F, 0x47;   // LD C,A : LD B,A ; 0, so length ignored
    inline 0x49, 0xDF;       // RST.LIS 18H
  end


  word halt () is
    inline 0xED, 0x7B, SP;   // LD   SP, (SP)
    inline 0x5B, 0xFD, 0xE1; // POP.LIL  IY
    inline 0x5B, 0xDD, 0xE1; // POP.LIL  IX
    inline 0x5B, 0xD1;       // POP.LIL  DE
    inline 0x5B, 0xC1;       // POP.LIL  BC
    inline 0x5B, 0xF1;       // POP.LIL  AF
    inline 0x21, 0, 0;       // LD   HL, 0
    inline 0x49;             // RET.LIS ; return to MOS
  end


  word  mul (word A, word B) is
    word  M:=0;
    while B >0 do
      word  T  :=A;
      word  I  :=1;
      while B-I>=I do
        T:=T+T;
        I:=I+I;
      end

      M:=M+T;
      B:=B-I;
    end

    return M;
  end


  word  div (word A, word B) is
    word  D:=0;
    while A>=B do
      word  T  :=B;
      word  I  :=1;
      while A-T>=T do
        T:=T+T;
        I:=I+I;
      end

      A:=A-T;
      D:=D+I;
    end

    return D;
  end


  word  mod (word A, word B) is
    return A-mul(div(A,B),B);
  end


  word Init () is
    Heap[ 0]:='0';
    Heap[ 1]:='1';
    Heap[ 2]:='2';
    Heap[ 3]:='3';
    Heap[ 4]:='4';
    Heap[ 5]:='5';
    Heap[ 6]:='6';
    Heap[ 7]:='7';
    Heap[ 8]:='8';
    Heap[ 9]:='9';
    Heap[10]:='A';
    Heap[11]:='B';
    Heap[12]:='C';
    Heap[13]:='D';
    Heap[14]:='E';
    Heap[15]:='F';

    Heap[16]:='c';
    Heap[17]:='h';
    Heap[18]:='a';
    Heap[19]:='r';
    Heap[20]:= char(0);

    Heap[21]:='b';
    Heap[22]:='y';
    Heap[23]:='t';
    Heap[24]:='e';
    Heap[25]:= char(0);

    Heap[26]:='w';
    Heap[27]:='o';
    Heap[28]:='r';
    Heap[29]:='d';
    Heap[30]:= char(0);

    Heap[31]:='b';
    Heap[32]:='e';
    Heap[33]:='g';
    Heap[34]:='i';
    Heap[35]:='n';
    Heap[36]:= char(0);

    Heap[37]:='i';
    Heap[38]:='f';
    Heap[39]:= char(0);

    Heap[40]:='w';
    Heap[41]:='h';
    Heap[42]:='i';
    Heap[43]:='l';
    Heap[44]:='e';
    Heap[45]:= char(0);

    Heap[46]:='i';
    Heap[47]:='n';
    Heap[48]:='l';
    Heap[49]:='i';
    Heap[50]:='n';
    Heap[51]:='e';
    Heap[52]:= char(0);

    Heap[53]:='r';
    Heap[54]:='e';
    Heap[55]:='t';
    Heap[56]:='u';
    Heap[57]:='r';
    Heap[58]:='n';
    Heap[59]:= char(0);

    Heap[60]:='e';
    Heap[61]:='n';
    Heap[62]:='d';
    Heap[63]:= char(0);
    nHeap   :=64;

    Name[ 0]:=16;
    Cls [ 0]:= 1;
    Size[ 0]:= 1;

    Name[ 1]:=21;
    Cls [ 1]:= 1;
    Size[ 1]:= 1;

    Name[ 2]:=26;
    Cls [ 2]:= 1;
    Size[ 2]:= 2;
    nName   := 3;

    pStk    := 0;
    nCode   := aCode;
    nData   := aData;
  end


  word  Push(word V) is
    Stk[pStk]:=V;
    pStk:=pStk+1;
  end


  word  Pop () is
    pStk:=pStk-1;
    return Stk[pStk];
  end


  byte puthexdig (byte dig) is
    if dig<10 then putc(char(dig+48)); end
    if dig>=10 then putc(char(dig+55)); end
  end

  word puthex (word hex) is
    pStk:=0;
    Push  (mod(hex,16));
    hex:=div(hex,16);
    Push  (mod(hex,16));
    hex:=div(hex,16);
    Push  (mod(hex,16));
    hex:=div(hex,16);
    while pStk!=0 do
      puthexdig(hex);
      hex := Pop();
    end
    puthexdig(hex);
  end


  word Stop () is
    close();

    pStk:=0;
    while    nLine!=0 do
      Push  (mod(nLine,10));
      nLine:=div(nLine,10);
    end

    while pStk!=0 do
      putc(char(Pop()+48));
    end

    halt();
  end


  word  val () is
    word E:=10;
    word I:= 0;
    if Buff[0]='0' then
      if Buff[1]='x' then
        E:=16;
        I:= 2;
      end
    end

    word N:=0;
    while Buff[I]!=char(0) do
      word K:=0;
      while Heap[K]!=Buff[I] do
        if K=E then
          Stop();
        end

        K:=K+1;
      end

      N:=mul(N,E)+K;
      I:=I+1;
    end

    return N;
  end


  char Look () is
    if pText>=nText then
      pText := 0;
      nText := read();
      if pText>=nText then
        return char(0);
      end
    end

    return Text[pText];
  end


  char  Read() is
    char   Ch:=Look();
    if     Ch =char(10) then
      nLine  :=nLine+1;
    end

    pText    :=pText+1;

    return Ch;
  end


  word  isalnum() is
    if 'A'<=Look() then
      if Look()<='Z' then
        return 0;
      end
    end

    if 'a'<=Look() then
      if Look()<='z' then
        return 0;
      end
    end

    if '0'<=Look() then
      if Look()<='9' then
        return 0;
      end
    end

    return     1;
  end


  word  Digraph(char C1, char C2) is
    if Buff[0]=C1 then
      if Look()=C2 then
        Buff[1]:=Read();
        Buff[2]:=char(0);
      end
    end
  end


  char  Scan() is
    word  pBuff:=0;
    while pBuff =0 do
      word  sFlag:=0;
      while sFlag =0 do
        if Look()!=char( 9) then
          if Look()!=char(10) then
            if Look()!=char(13) then
              if Look()!=char(32) then
                sFlag:=1;
              end
            end
          end
        end

        if sFlag=0 then
          Read();
        end
      end

      while isalnum()=0 do
        Buff[pBuff]:= Read();
        pBuff      :=pBuff+1;
      end

      if pBuff=0 then
        Buff[pBuff]:= Read();
        pBuff      :=pBuff+1;
      end

      Buff[pBuff]  :=char(0);

      Digraph('<','=');
      Digraph('!','=');
      Digraph('>','=');
      Digraph(':','=');

      if Buff[0]='/' then
        if Look()='/' then
          while Look()!=char(10) do
            if Read()=char(0) then
              Stop();
            end
          end

          pBuff:=0;
        end
      end
    end
  end


  word  Comp(word pHeap) is
    word pBuff:=0;
    while Buff[pBuff]=Heap[pHeap] do
      if Buff[pBuff]=char(0) then
        return 0;
      end

      pHeap:=pHeap+1;
      pBuff:=pBuff+1;
    end

    return     1;
  end


  word  Find(word fFlag) is
    word  pName:=0;
    while pName< nName do
      if Comp(Name[pName])=0 then
        return pName;
      end

      pName:=pName+1;
    end

    if fFlag=0 then
      Stop();
    end

    return     pName;
  end


  byte  Emi1(byte B) is
    Code[nCode-aCode]:=B;
    nCode:=nCode+1;
  end


  word  Emi2(word W) is
    Emi1(mod(W,256));
    Emi1(div(W,256));
  end


  word  Emi3(word P, word W) is
    Code[P]  :=mod(W,256);
    Code[P+1]:=div(W,256);
  end


  word  MkBX(word I) is
    if Size[Type[I]]=2 then
      Emi1(0xEB);            // xchg

      Emi1(0x29);            // dad  H

      Emi1(0x11);            // lxi  D, Val
      Emi2(Ofs[I]);
    end

    if Size[Type[I]]=1 then
      Emi1(0x21);            // lxi  H, Val
      Emi2(Ofs[I]);
    end

    Emi1(0x19);              // dad  D
  end


  word  Mov (word E, word D, word I) is
    if Size[I]=1 then
      Emi1(0x21);            // lxi  H, Val
      Emi2(Ofs[I]);
    end

    Emi1(E);                 // mov  E, M (mov  M, E)

    if Size[Type[I]]=2 then
      Emi1(0x23);            // inx  H

      Emi1(D);               // mov  D, M (mov  M, D)
    end
  end


  word  Assign(word I) is
    if Size[I]>1 then
      Emi1(0xE1);            // pop  H
    end

    Mov (0x73,0x72,I);
  end


  word  Expr() is
    word eFlag:=0;
  //if   eFlag =0 then
      if '0'<=Buff[0] then
        if Buff[0]<='9' then
          Emi1(0x11);        // lxi  D, Val
          Emi2(val());

          eFlag:=1;
        end
      end

      if Buff[0]=''' then
        Emi1(0x11);          // lxi  D, Val
        Emi2(word(Read()));

        Read();

        eFlag:=1;
      end

      if eFlag=0 then  // 1.13
        if Buff[0]='"' then
          word pCode:=nCode;

          Emi1(0xC3);        // JP   ?
          nCode:=nCode+2;

          while Look()!='"' do
            if Look()=char( 0) then
              Stop();
            end

            Emi1(word(Read()));
          end

          Emi1(0);
          Read();

          Emi3(pCode+1, nCode);
          Emi1(0x11);        // LD   DE, Str
          Emi2(pCode+3);

          eFlag:=1;
        end
      end

      if Buff[0]='(' then
        Scan();
        Expr();

        eFlag:=1;
      end
  //end

    if   eFlag =0 then
      word   I:=Find(0);
      if Cls[I]=1 then
        Push(I);
        Scan();              // (
        Scan();
        Expr();
        I:=Pop();
      end

      if Cls[I]=2 then
        if Size[I]>1 then
          Push(I);
          Scan();            // [
          Scan();
          Expr();
          I:=Pop();

          MkBX(I);
        end

        Mov (0x5E,0x56,I);

        if Size[Type[I]]=1 then
          Emi1(0x16);        // mvi  D, 0
          Emi1(0x00);
        end
      end

      if Cls[I]=3 then
        Scan();              // (

        Push(I);

        Sub[nName]:= 0;

        word J:=I+1;
        while Sub[J]=1 do
          Push(J);
          Scan();
          Expr();
          J:=Pop();

          Assign(J);

          J:=J+1;
        end

        I:=Pop();
        if J=I+1 then
          Scan();            // )
        end

        Emi1(0xCD);          // call Ofs
        Emi2(Ofs[I]);
      end
    end

    Scan();

    byte Op1:=0;
    byte Op2:=0;
    if Buff[0]='+' then
      Op1:=0x83;             // add  E
      Op2:=0x8A;             // adc  D
    end

    if Buff[0]='-' then
      Op1:=0x93;             // sub  E
      Op2:=0x9A;             // sbb  D
    end

    if   Op1!=0 then
      Emi1(0xD5);            // push D

      Push(Op1);
      Push(Op2);
      Scan();
      Expr();
      Op2:=Pop();
      Op1:=Pop();

      Emi1(0xE1);            // pop  H

      Emi1(0x7D);            // mov  A, L
      Emi1(Op1);             // op1  E
      Emi1(0x5F);            // mov  E, A

      Emi1(0x7C);            // mov  A, H
      Emi1(Op2);             // op2  D
      Emi1(0x57);            // mov  D, A
    end
  end


  word  Jump(byte jCode, word jOfs) is
    if jOfs=0 then
      Push(nCode);
    end

    Emi1(jCode);             // jxx  Addr
    Emi2(nCode+jOfs);
  end


  word  Cond() is
    Scan();
    Expr();

    word jType :=   0;
    word jOfs1 :=   0;
    word jCode2:=0xD2;       // jnc
    if Buff[0]='<' then
      jType    :=   1;
      if Buff[1]='=' then
        jType  :=   2;
        jCode2 :=0xDA;       // jc
      end
    end

    if Buff[0]='=' then
      jType    :=   4;
      jCode2   :=0xC2;       // jnz
    end

    if Buff[0]='!' then
      jType    :=   3;
      jOfs1    :=   7;
      jCode2   :=0xCA;       // jz
    end

    if Buff[0]='>' then
      jType    :=   2;
      if Buff[1]='=' then
        jType  :=   1;
        jCode2 :=0xDA;       // jc
      end
    end

    if jType=0 then
      Stop();
    end

    Emi1(0xD5);              // push D

    Scan();
    Expr();

    Emi1(0xE1);              // pop  H

    if jType=1 then
      Emi1(0x7D);            // mov  A, L
      Emi1(0x93);            // sub  E
      Emi1(0x7C);            // mov  A, H
      Emi1(0x9A);            // sbb  D
    end

    if jType=2 then
      Emi1(0x7B);            // mov  A, E
      Emi1(0x95);            // sub  L
      Emi1(0x7A);            // mov  A, D
      Emi1(0x9C);            // sbb  H
    end

    if jType>=3 then
      Emi1(0x7D);            // mov  A, L
      Emi1(0x93);            // sub  E
      Jump(0xC2, jOfs1);     // jnz
      Emi1(0x7C);            // mov  A, H
      Emi1(0x92);            // sub  D
    end

    if jType!=4 then
      Push(nCode);
    end

    Jump  (jCode2, 0x00);
  end


  word  Obj (word T) is
    if Cls[T]!=1 then
      Stop();
    end

    Name[nName]:=nHeap;
    Type[nName]:= T;

    Scan();
    if Find(1)<nName then
      Stop();
    end

    word pBuff:=0;
    char  Ch  :=char(1);
    while Ch  !=char(0) do
      Ch         := Buff[pBuff];
      Heap[nHeap]:= Ch;
      nHeap      :=nHeap+1;
      pBuff      :=pBuff+1;
    end

    Scan();

    return nName;
  end


  char  Var (word Subclass) is
    Cls [nName]:= 2;
    Sub [nName]:= Subclass;
    Size[nName]:= 1;
    Ofs [nName]:=nData;

    if Buff[0]='[' then
      if Subclass!=0 then
        Stop();
      end

      Scan();
      Size[nName]:=val();
      Scan();                // ]
      Scan();                // ;
    end

    nData      :=nData+mul(Size[nName],Size[Type[nName]]);
    nName      :=nName+1;

    return Buff[0];
  end


  word  Hide() is
    word  I:=Pop();
    while I< nName do
      Heap[Name[I]]:=char(0);
      I :=I+1;
    end
  end


  word rFlag;

  word  Ctrl() is
    word cFlag:=0;
  //if   cFlag =0 then
      if Comp(37)=0 then     // if
        Cond();

        Push(nName);

        Scan();
        Ctrl();
        while Comp(60)!=0 do // !end
          Ctrl();
        end

        Hide();

        Emi3(Pop()+1,nCode);
        Emi3(Pop()+1,nCode);

        rFlag:=1;            // 14.05.2006
        cFlag:=1;
      end
  //end

    if   cFlag =0 then
      if Comp(40)=0 then     // while
        Push(nCode);

        Cond();

        Push(nName);

        Scan();
        Ctrl();
        while Comp(60)!=0 do // !end
          Ctrl();
        end

        Hide();

        word pExit2:=Pop();
        word pExit1:=Pop();
        word pLoop :=Pop();

        Emi1(0xC3);          // jmp  Ofs
        Emi2(pLoop);

        Emi3(pExit1+1,nCode);
        Emi3(pExit2+1,nCode);

        rFlag:=1;            // 14.05.2006
        cFlag:=1;
      end
    end

    if   cFlag =0 then
      if Comp(46)=0 then     // inline
        Buff[0]:=',';
        while Buff[0]=',' do
          Scan();
          cFlag:=0;
          if '0'<=Buff[0] then
            if Buff[0]<='9' then
              cFlag:=1;
            end
          end
          if cFlag=1 then Emi1(val());        end  // db Val
          if cFlag=0 then Emi2(Ofs[Find(0)]); end  // dw Adr
          Scan();
        end

        rFlag:=1;            // 14.05.2006
        cFlag:=1;
      end
    end

    if   cFlag =0 then
      if Comp(53)=0 then     // return
        Scan();
        Expr();

        Emi1(0xC9);          // ret

        rFlag:=0;            // 14.05.2006
        cFlag:=1;
      end
    end

    if   cFlag =0 then
      word   I:=Find(0);
      if Cls[I]=1 then
        word N:=Obj(I);
        if   Var(2)=':' then
          Scan();
          Expr();

          Assign(N);
        end
      end

      if Cls[I]=2 then
        if Size[I]>1 then
          Scan();            // [
          Scan();
          Expr();

          MkBX(I);

          Emi1(0xE5);        // push H
        end

        Scan();              // :=
        Scan();
        Expr();

        Assign(I);
      end

      if Cls[I]=3 then
        Expr();
      end

      rFlag:=1;              // 14.05.2006
    end

    Scan();
  end


  word Func () is
    Scan();
    Ctrl();
    while Comp(60)!=0 do     // !end
      Ctrl();
    end
  end


//word puts () is
//  word n := 0;
//  while Dest[n] != char(0) do putc(Dest[n]); n:=n+1; end
//end

word GetCmdLine () is
  word dotPos := 0;

  word  I:=0;
  while Command[I] = ' ' do
    I:=I+1;
  end

  word  K:=0;
  char ch := Command[I];
  while ch != char(0) do
    Src[K] := ch; Dest[K] := ch;
    K:=K+1;

    //if   K>=nBUFF then
    //  Stop(); // @eLONGFILENAME
    //end

    I:=I+1;
    ch := Command[I];
    if ch = ' ' then ch := char(0); end
    if ch = '.' then dotPos:= K; end
  end
  Src[K] := char(0); Dest[K] := char(0);

  if Src[0] != char(0) then

    if dotPos = 0 then
      dotPos := K;
      Src[K] := '.'; K:=K+1;
      Src[K] := 'c'; K:=K+1;
      Src[K] := 't'; K:=K+1;
      Src[K] := 'x'; K:=K+1;
      Src[K] := char(0);
    end

    while Command[I] = ' ' do
      I:=I+1;
    end

    K := 0;
    ch := Command[I];
    while ch != char(0) do
      Dest[K] := ch;
      K:=K+1;

      //if   K>=nBUFF then
      //  Stop(); // @eWRONGPARM
      //end

      I:=I+1;
      ch := Command[I];
      if ch = ' ' then ch := char(0); end
    end

    if K = 0 then
      Dest[dotPos] := '.';
      dotPos := dotPos+1;
      Dest[dotPos] := 'b';
      dotPos := dotPos+1;
      Dest[dotPos] := 'i';
      dotPos := dotPos+1;
      Dest[dotPos] := 'n';
      K := dotPos + 1;
    end
    Dest[K] := char(0);

  end
end


begin
  inline 0xED, 0x73, SP;  // LD   (SP), SP

  inline 0x11, Command;   // LD   DE, Command
  inline 0x5B, 0x7E;      // LOOP: LD.LIL A, (HL)
  inline 0x12;            // LD   (DE), A
  inline 0x5B, 0x23;      // INC.LIL HL
  inline 0x13;            // INC   DE
  inline 0xB7;            // OR   A
  inline 0x20, 0xF7;      // JR   NZ, LOOP

  aCode := 0x0000;
  aData := 0x8000;

  GetCmdLine();
  if Src[0] = char(0) then
    puts("TinyContext v1.0 for Z80");
    putc(char(13)); putc(char(10));
    puts("ctx prog[.ctx] [prog.bin]");
    putc(char(13)); putc(char(10));
    halt();
  end

  Init();

  hFile := open();
  if hFile = 0 then puts("can't open source"); halt(); end

    pText:=0;
    nText:=0;
    nLine:=1;

    Emi1(0xC3);       // jp start
    nCode:=nCode+2;

    while nCode!=64 do Emi1(0x00); end  // .align 64
    Emi1(byte('M'));  // MOS header
    Emi1(byte('O'));
    Emi1(byte('S'));
    Emi1(0x00);
    Emi1(0x00);  // run mode (0: Z80, 1: ADL)

    Scan();
    while Comp(31)!=0 do     // !begin
      Obj (Find(0));
      char Ch:=Buff[0];
      if   Ch ='(' then
        Cls [nName]:= 3;
        Sub [nName]:= 0;
        Ofs [nName]:=nCode;
        nName      :=nName+1;

        Push(nName);

        Scan();
        if Buff[0]!=')' then
          Obj (Find(0));
          while Var(1)=',' do
            Scan();
            Obj (Find(0));
          end
        end

        Scan();              // is
        Func();
        if rFlag!=0 then     // 14.05.2006 // if rFlag=0 then
          Emi1(0xC9);        // ret
        end
        Hide();
      end

      if   Ch!='(' then
        Var(0);
      end

      Scan();
    end

    Emi3 (1,nCode);

    Emi2 (0xF55B);  // PUSH.LIL AF
    Emi2 (0xC55B);  // PUSH.LIL BC
    Emi2 (0xD55B);  // PUSH.LIL DE
    Emi1 (0x5B);
    Emi2 (0xE5DD);  // PUSH.LIL IX
    Emi1 (0x5B);
    Emi2 (0xE5FD);  // PUSH.LIL IY
    Func ();
    Emi1 (0x5B);
    Emi2 (0xE1FD);  // POP.LIL IY
    Emi1 (0x5B);
    Emi2 (0xE1DD);  // POP.LIL  IX
    Emi2 (0xD15B);  // POP.LIL  DE
    Emi2 (0xC15B);  // POP.LIL  BC
    Emi2 (0xF15B);  // POP.LIL  AF
    Emi1 (0x21);
    Emi2 (0);       // LD   HL, 0
    Emi2 (0xC949);  // RET.LIS ; return to MOS

    close();

    puts("code "); puthex(aCode); putc('-'); puthex(nCode-1);
    putc(':'); puthex(nCode-aCode); putc(char(13)); putc(char(10));
    puts("data "); puthex(aData); putc('-'); puthex(nData-1);
    putc(':'); puthex(nData-aData); putc(char(13)); putc(char(10));

    if save() != 0 then putc('S'); end

  end
